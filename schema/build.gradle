plugins {
	id 'org.springframework.boot' version '2.5.3'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'org.openapi.generator' version "4.3.1"
	id 'java'
	id 'maven-publish'
}

group = 'com.nutty'
version = '0.6.0'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

// needed??
dependencyManagement {
	imports {
		mavenBom "org.springframework.boot:spring-boot-starter-parent:2.5.3"
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation "io.swagger.parser.v3:swagger-parser:2.0.20"
	implementation "org.openapitools:jackson-databind-nullable:0.2.1"
}

publishing {
	publications {
		mavenJava(MavenPublication){
//			components.java
			artifact jar
		}
	}

	repositories {
		mavenLocal()
	}
}

test {
	useJUnitPlatform()
}

openApiGenerate {
	def namespace = "${group}.${rootProject.name}"

	generatorName = "spring"
	inputSpec = "$projectDir/src/main/index.yaml"
	outputDir = "$buildDir/generated"
	apiPackage = "${namespace}.api"
	invokerPackage = "${namespace}"
	modelPackage = "${namespace}.models"
	configOptions = [
			dateLibrary: "java8",
			interfaceOnly: "true",
	]
}

jar {
	enabled = true
	archiveClassifier = ''
}

bootJar{
	enabled = false
}

compileJava.dependsOn tasks.openApiGenerate
sourceSets.main.java.srcDir "$buildDir/generated/src/main/java"
sourceSets.main.resources.srcDir "$buildDir/generated/src/main/resources"